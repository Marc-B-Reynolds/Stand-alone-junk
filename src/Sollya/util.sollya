/* -*- mode: c; -*- */

if (!isbound(_util_library_loaded)) then {

_util_library_loaded = true;

procedure coeff_f32(p)
{
  return roundcoefficients(p,[|24...|]);
};

 procedure coeff_f64(p)
{
  return roundcoefficients(p,[|53...|]);
};

procedure within_dx(a,b,d)
{
  return abs(a-b) < d;
};

// first 'n' odd values
procedure odd_list(n)
{
  var c,l,i;
  c = 2*n;
  l = [||];  

  for i from 1 to c by 2 do l = l:.i;

  return l;
};

// first 'n' even values
procedure even_list(n)
{
  var c,l,i;
  c = 2*n-1;
  l = [||];

  for i from 0 to c by 2 do l = l:.i;

  return l;
};

procedure full_list(n)
{
  var c,l,i;
  l = [||];  

  for i from 0 to n-1 do l = l:.i;

  return l;
};


procedure to_csv_string(l=...)
{
  var s,e,i;

  s = "";
  e = length(l)-2;

  for i from 0 to e do {
    s = s @ l[i] @ ",";
  };

  s = s @ l[e+1];

  return s;
};

u_round_f32 = proc(n) { return single(n); };
u_round_f64 = proc(n) { return double(n); };

// make a copy of list with f applied
procedure list_apply(l,f)
{
  var r,v,i;

  r = [||];

  for i from 0 to length(l)-1 do {
   v = f(l[i]);
   r = r :. v;
  };

  return r;
};


// make a copy of list as binary32
procedure list_to_f32(l) { return list_apply(l, u_round_f32); };

 // make a copy of list as binary64
procedure list_to_f64(l)
{
  var r,v,i;

  r = [||];

  for i from 0 to length(l)-1 do {
   v := double(l[i]);
   r  = r :. v;
  };

  return r;
};

// return true if 'v' is in list 'l'
procedure is_in_list(l, v)
{
  var i,r,n;

  i = 0;
  r = false;
  n = length(l);
  
  while ((i < n) && (!r)) do {
    if (l[i] == v) then {r = true;};
    i := i + 1;
  };

  return r;
};
 
 
// scale elem of 'l' by 's' and convert to binary32
procedure scale_list_to_f32(l,s)
{
  var r,v,i;

  r = [||];

  for i from 0 to length(l)-1 do {
   v := s*single(l[i]);
   r  = r :. v;
  };

  return r;
};

// for error testing
procedure fma(a,b,c)  { double(a*b+c); };
procedure fmaf(a,b,c) { single(a*b+c); };

procedure f64_ufp(v)  { v = double(v); return 2^(exponent(v)+precision(v)- 1); };
procedure f64_ulp(v)  { v = double(v); return 2^(exponent(v)+precision(v)-53); };
procedure f64_succ(v) { v = double(v); return single(v+f64_ulp(v)); };
procedure f64_pred(v) { v = double(v); return single(v-f64_ulp(v)); };

procedure f32_ufp(v)  { v = single(v); return 2^(exponent(v)+precision(v)- 1); };
procedure f32_ulp(v)  { v = single(v); return 2^(exponent(v)+precision(v)-24); };
procedure f32_succ(v) { v = single(v); return single(v+f32_ulp(v)); };
procedure f32_pred(v) { v = single(v); return single(v-f32_ulp(v)); };

 
};

